// define java 8 as a minimum source and target versions
apply plugin: 'idea'
apply plugin: 'java'
compileJava {
    targetCompatibility = "1.8"
    sourceCompatibility = "1.8"
    options.encoding = 'UTF-8'
}

// Just a smoke test that using this option does not lead to any exception
compileJava.options.compilerArgs = ['-Xlint:unchecked']

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

ext {
    log4jVersion = "2.4.1"
}

dependencies {

    // logging settings
    compile("org.apache.logging.log4j:log4j-api:${log4jVersion}")
    compile("org.apache.logging.log4j:log4j-core:${log4jVersion}")
    compile "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    compile "org.apache.logging.log4j:log4j-jcl:${log4jVersion}"
    compile "org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}"
    compile "org.apache.logging.log4j:log4j-web:${log4jVersion}"

    // utilities
    compile("com.google.guava:guava:19.0")

    testCompile("junit:junit:4.12")
}

idea.module.iml {
    beforeMerged { module ->
        module.dependencies.clear()
    }
    whenMerged { module ->
        module.dependencies*.exported = true
    }
}

idea.project.ipr {
    beforeMerged { project ->
        project.modulePaths.clear()
    }
    withXml { provider ->
        provider.node.component.find {
            it.@name == 'VcsDirectoryMappings'
        }.mapping.@vcs = 'Git'
    }
}